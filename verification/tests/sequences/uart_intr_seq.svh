//Copyright (C) 2021 Intel Corporation
//SPDX-License-Identifier: MIT
//===============================================================================================================
/**
 * Abstract:
 * class uart_intr_seq is executed by uart_intr_test 
 * 
 * This sequence verifies the functionality of the UART interrupt 
 * The interrupt is generated by forcing / writing the error register 
 * PBA mechanism is verified using masking and un-masking the interrupt vector
 * Sequence is running on virtual_sequencer 
 *
 *
**/
//==============================================================================================================



`ifndef UART_INTR_SEQ_SVH
`define UART_INTR_SEQ_SVH

class uart_intr_seq extends base_seq;
    `uvm_object_utils(uart_intr_seq)
    `uvm_declare_p_sequencer(virtual_sequencer)

  rand bit[63:0]    intr_addr;
  rand bit[63:0]    intr_wr_data;
  rand bit[63:0]    ras_err_code;//1:CatastError, 2:FatalError, 4:NoFatalError
  rand bit[63:0]    fme_err_code;//1:PartialReconfigFIFOParityErr, 2:RemoteSTPParityErr, 32:AfuAccessModeErr
  rand bit inj_ras_err; 
  rand bit inj_fme_err; 
  static int e_set;

  rand bit [63:0] dut_mem_start;
  rand bit [63:0] dut_mem_end;

  constraint addr_cons {
     dut_mem_end > dut_mem_start;
     intr_addr[7:0] == 0;
     intr_addr   >= dut_mem_start;
     intr_addr    < dut_mem_end;
     intr_addr[63:32] == 32'b0;
  }
    
  constraint wr_dat_cons {
     !(intr_wr_data inside {64'h0});
      intr_wr_data[63:32] == 32'b0; 
  }

 
    function new(string name = "uart_intr_seq");
        super.new(name);
    endfunction : new

    task body();
        bit [63:0] wdata, rdata, addr, intr_masked_data;
	bit [31:0] host_intr_rdata[16];
        bit [63:0] afu_id_l, afu_id_h;
        bit msix_req_set;


        super.body();
        `uvm_info(get_name(), "Entering uart_intr_seq...", UVM_LOW)

        repeat(3)begin

  	  this.randomize() with{dut_mem_start == tb_cfg0.dut_mem_start && dut_mem_end == tb_cfg0.dut_mem_end;};
          `uvm_info(get_name(), $psprintf("TEST: dut_mem_start=%0h dut_mem_end=%0h", dut_mem_start, dut_mem_end), UVM_LOW)

          if(fme_err_code == 20'h00001) e_set = 0;

          `uvm_info(get_name(), $psprintf("TEST: Configure MSIX Table BAR0 MSIX_ADDR5/MSIX_CTLDAT5"), UVM_LOW)
          `uvm_info(get_name(), $psprintf("TEST: MMIO WRITE to MSIX_ADDR5"), UVM_LOW)
          mmio_write64(.addr_(tb_cfg0.PF0_BAR4+FME_MSIX_BASE_ADDR+20'h0_0050), .data_(intr_addr));
          #1us;

          `uvm_info(get_name(), $psprintf("TEST: MMIO WRITE to MSIX_CTLDAT5 with masked Interrupt"), UVM_LOW)
          intr_masked_data[31:0] = intr_wr_data[31:0];
          intr_masked_data[63:32] = 32'b1; 
          mmio_write64(.addr_(tb_cfg0.PF0_BAR4+FME_MSIX_BASE_ADDR+20'h0_0058), .data_(intr_masked_data));
          #25us;
 
          `uvm_info(get_name(), $psprintf("TEST: Inject UART interrupt"), UVM_LOW)
	      force `UART_TOP.uart_irq_50m = 1'b1 ;

	   #1us;
          `uvm_info(get_name(), $psprintf("TEST: Check MSIX_PBA[5] is set for masked UART interrupt"), UVM_LOW)
          for(int i=0;i<200;i++) begin
            mmio_read64(.addr_(tb_cfg0.PF0_BAR4+FME_MSIX_BASE_ADDR+20'h0_0070),.data_(rdata));
            if(rdata[5]) break;
            #1ns;
          end
          assert(rdata[5]) else 
            `uvm_error(get_type_name(),$sformatf("TEST : MSIX_PBA[5] not set post masked interrupt"))

          `uvm_info(get_name(), $psprintf("TEST: Unmasked UART interrupt by writing on MSIX_CTLDAT5[63:32]"), UVM_LOW)
          mmio_write64(.addr_(tb_cfg0.PF0_BAR4+FME_MSIX_BASE_ADDR+20'h0_0058), .data_(intr_wr_data));

	
          #1us;
          `uvm_info(get_name(), $psprintf("TEST: Check MSIX_PBA[5] is clear after asserting pending UART interrupt"), UVM_LOW)
           mmio_read64(.addr_(tb_cfg0.PF0_BAR4+ FME_MSIX_BASE_ADDR +20'h0_0070),.data_(rdata));
          assert(rdata[5]==0) else 
            `uvm_error(get_type_name(),$sformatf("TEST : MSIX_PBA[5] is not clear after asserting pending UART interrupt"));

          `uvm_info(get_name(), $psprintf("TEST: Read Host memory"), UVM_LOW)
          host_mem_read( .addr_(intr_addr) , .data_(host_intr_rdata) , .len('d16) );

          if(changeEndian(host_intr_rdata[0]) !== intr_wr_data)
              `uvm_error(get_name(), $psprintf("Interrupt write data mismatch exp = %0h act = %0h", intr_wr_data, changeEndian(host_intr_rdata[0])))
          else
              `uvm_info(get_name(), $psprintf("TEST: Interrupt data match intr_addr=%0h intr_wr_data = %0h", intr_addr, intr_wr_data), UVM_LOW)

          release `UART_TOP.uart_irq_50m; 

          msix_req_set = 0;
          #1us;
        end

       `uvm_info(get_name(), "Exiting uart_intr_seq...", UVM_LOW)

    endtask : body

endclass : uart_intr_seq

`endif // uart_intr_SEQ_SVH
